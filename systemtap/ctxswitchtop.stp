#!/usr/bin/env stap
# Ctxswitchtop counts the number of context switches of each pid and prints out a
# sorted list of pids with the most context switches during a 5 second period.
#
# Note, if you are comparing this cs count with vmstat you need to divide the number by 5
# as vmstat give you cs/s.

# Filetop prints out which files are being read / written to globally on your system.
# Every 5 seconds it prints out how many kilobytes has been read or written to a particular file.
global pids, names


probe begin {
    printf("Gathering data...\n")
}


probe scheduler.ctxswitch {
    pids[prev_pid] += 1
    names[prev_pid] = prev_task_name
}


probe timer.sec(5) {
    ansi_clear_screen()
    ansi_set_color3(30, 47, 0)
    printf("\n%-16s %-16s %-10s\n", "Name", "Pid", "Count")
    ansi_reset_color()


    total = 0
    foreach (pid in pids- limit 40) {
        printf("%-16s %-16d %-10d\n", names[pid], pid, pids[pid])
        total += pids[pid]
    }
    printf("\n\nTotal: %d", total)
    delete pids
    delete names
}

